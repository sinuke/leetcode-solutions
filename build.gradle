plugins {
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'io.freefair.lombok' version '8.14.3'
    id 'jacoco'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'io.freefair.lombok'

    jacoco {
        toolVersion = "0.8.14"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    group = 'com.sinuke.leetcode'
    version = '1.0'

    repositories {
        mavenCentral()
    }

    ext {
        version_testcontainers = '2.0.2'
        version_junit = '6.0.1'
        version_jackson = '3.0.2'
    }

    dependencies {
        implementation group: 'tools.jackson.core', name: 'jackson-databind', version: version_jackson
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.21'

        testImplementation platform("org.junit:junit-bom:${version_junit}")
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: version_junit
        testImplementation group: 'org.testcontainers', name: 'testcontainers-junit-jupiter', version: version_testcontainers
        testImplementation group: 'org.testcontainers', name: 'testcontainers-mysql', version: version_testcontainers
        testImplementation group: 'com.mysql', name: 'mysql-connector-j', version: '9.5.0'

        testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: version_junit
    }

    test {
        useJUnitPlatform()
        testlogger {
            theme = 'mocha'
        }
    }

    tasks.withType(Test).tap { configureEach
            {
                finalizedBy jacocoTestReport
            }
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }

}

tasks.register('jacocoRootReport', JacocoReport) {
    dependsOn subprojects.test

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)

    classDirectories = classDirectories.filter { file ->
        !file.path.contains('common') // exclude "common" module from report
    }

    reports {
        xml.required = true
        html.required = true
    }
}